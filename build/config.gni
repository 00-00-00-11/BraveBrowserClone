import("//build/toolchain/toolchain.gni")
import("//build/util/branding.gni")

declare_args() {
  brave_product_name = ""
  brave_project_name = ""
  executable_name = ""
  brave_version_major = ""
  brave_version_minor = ""
  brave_version_build = ""
  brave_version_patch = 0
}

brave_version = "$brave_version_major.$brave_version_minor.$brave_version_build"

if (executable_name == "") {
  executable_name = brave_project_name
}

brave_dist_dir = "$root_out_dir/dist"
brave_exe = executable_name
if (is_win) {
  brave_exe = "$brave_exe.exe"
} else if (is_mac) {
  brave_exe = "$chrome_product_full_name.app"
}

if (is_linux) {
  # Our channle name and upstream linux channel name is different.
  brave_linux_channel = ""
  if (brave_channel == "") {
    brave_linux_channel = "stable"
  } else if (brave_channel == "dev") {
    brave_linux_channel = "unstable"
  } else {
    assert(brave_channel == "beta", "Not supported channel name: $brave_channel")
  }
}

brave_icon_dir = "nightly"
if (is_component_build) {
  brave_icon_dir = "dev"
}

brave_platform = "darwin"
if (is_win) {
  brave_platform = "win32"
} else if (is_linux) {
  brave_platform = "linux"
}

brave_product_dir_name_suffix = ""

if (is_official_build) {
  if (brave_channel == "beta") {
    brave_product_dir_name_suffix = "-Beta"
  } else if (brave_channel == "dev") {
    brave_product_dir_name_suffix = "-Dev"
  } else if (brave_channel == "nightly") {
    brave_product_dir_name_suffix = "-Nightly"
  } else {
    assert(brave_channel == "", "Unknown channel name")
  }
} else {
  brave_product_dir_name_suffix = "-Development"
}

brave_product_dir_name = "BraveSoftware/Brave-Browser$brave_product_dir_name_suffix"
