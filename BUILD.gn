import("//tools/grit/repack.gni")

group("child_dependencies") {
  public_deps = [
    "//brave/renderer",
    "//brave/utility",
    "common"
  ]
  deps = [
    "chromium_src:common",
    "chromium_src:renderer",
  ]
}

group("browser_dependencies") {
  public_deps = [
    "//brave/browser",
    "common",
    ":brave_framework_resources",
    "chromium_src:browser",
    "chromium_src:common",
  ]
}

group("brave") {
  public_deps = [
    "//chrome",
  ]
  if (is_win) {
    deps = [
      "build/win:copy_exe",
      "build/win:copy_pdb"
    ]
  }
}

repack("packed_extra_resources") {
  sources = [
    "$root_gen_dir/components/brave_components_resources.pak",
    "$target_gen_dir/browser/resources/brave_extension.pak",
  ]

  deps = [
    "//brave/components/resources:brave_components_resources_grit",
    "//brave/browser/resources:brave_extension_grit"
  ]

  deps += [
    "//brave/common/extensions/api"
  ]

  if (is_mac) {
    output = "$root_gen_dir/repack/brave_resources.pak"
  } else {
    output = "$root_out_dir/brave_resources.pak"
  }
}

group("packed_resources") {
  public_deps = [
    ":packed_extra_resources",
  ]
}

brave_framework_public_deps = [
  ":packed_resources",
]

if (is_mac) {
  bundle_data("brave_framework_resources") {
    sources = [
      "$root_gen_dir/repack/brave_resources.pak",
    ]
    public_deps = brave_framework_public_deps

    outputs = [
      "{{bundle_resources_dir}}/{{source_file_part}}",
    ]
  }
} else {
  group("brave_framework_resources") {
    public_deps = brave_framework_public_deps
  }
}

group("create_dist") {
  deps = []
  if (is_win) {
    deps += [
      "//chrome/installer/mini_installer"
    ]
  }
  if (is_mac) {
    deps += [ "build/mac:create_dist_mac" ]
  }
}
